plugins {
    id "java"
    alias libs.plugins.josm
    id "maven-publish"
}

version = libs.versions.libphonenumber.get()

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/gabortim/josm-libphonenumber"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(libs.versions.javaLauncher.get())
    }
}

compileJava {
    // https://docs.gradle.org/current/userguide/building_java_projects.html#sec:compiling_with_release
    options.release = libs.versions.javaTarget.get().toInteger()
}

josm {
    pluginName = "libphonenumber"
    josmCompileVersion = libs.versions.josmCompile.get()
    manifest {
        author = project.pluginAuthor
        description = project.pluginDescription
        minJosmVersion = libs.versions.minJosm.get()
        minJavaVersion = libs.versions.javaTarget.get().toInteger()
        canLoadAtRuntime = true
        mainClass = "org.openstreetmap.josm.plugins.libphonenumber.LibphonenumberPlugin"
        website = new URI(project.pluginWebsite).toURL()
    }
}

dependencies {
    // https://mvnrepository.com/artifact/com.googlecode.libphonenumber/libphonenumber
    packIntoJar(libs.libphonenumber)

    // https://mvnrepository.com/artifact/com.googlecode.libphonenumber/geocoder
    packIntoJar(libs.geocoder)
}

jar {
    from("LICENSE")
    from("README.md")
}