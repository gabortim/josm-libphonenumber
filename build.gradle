plugins {
    id "java"
    id "org.openstreetmap.josm" version "0.8.2"
    id "maven-publish"
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/gabortim/josm-libphonenumber"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    // https://docs.gradle.org/current/userguide/building_java_projects.html#sec:compiling_with_release
    options.release = 17
}

version = "9.0.10"
String versionFile = "version.txt"

josm {
    pluginName = "libphonenumber"
    josmCompileVersion = "18622"
    manifest {
        author = "gaben"
        description = "Provides Google libphonenumber library components. Not meant to be installed directly by users, but rather as a dependency for other plugins."
        minJosmVersion = "17428"
        minJavaVersion = 17
        canLoadAtRuntime = true
        mainClass = "org.openstreetmap.josm.plugins.libphonenumber.LibphonenumberPlugin"
        website = new URI("https://github.com/gabortim/josm-libphonenumber").toURL()
    }
}

dependencies {
    // https://mvnrepository.com/artifact/com.googlecode.libphonenumber/libphonenumber
    packIntoJar("com.googlecode.libphonenumber:libphonenumber:$version")

    // https://mvnrepository.com/artifact/com.googlecode.libphonenumber/geocoder
    packIntoJar("com.googlecode.libphonenumber:geocoder:3.10")
}

jar {
    from("LICENSE")
    from("README.md")
}

tasks.register("storeVersion") {
    doLast {
        new File(versionFile).text = "$version"
    }
}
tasks.jar.dependsOn("storeVersion")

tasks.clean {
    delete(versionFile)
}